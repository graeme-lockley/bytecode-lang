let Bytecode = import("./bytecode.rebo")
let Parser = import("./parser.rebo")

let compile(ast) {
   let bc = _codegen()
   let env = {}
   let globalOffset = 0
   let patchAddress = ()

   ast |> each(fn(stmt)
      match stmt
      | { kind: "Var", name, value } -> {
         if patchAddress != () -> {
            bc.patchInt(patchAddress, bc.address())
            patchAddress := ()
         }

         if env[name] != () -> raise { kind: "CompilerError", reason: "compile: Variable already defined", ast: stmt }

         _compileIntExpr(bc, env, value)

         env[stmt.name] := { kind: "Var", offset: globalOffset }
         globalOffset := globalOffset + 1
      }
      | { kind: "Fn", name, params, body } -> {
         if patchAddress == () -> {
            bc.appendInstruction(Bytecode.JMP)
            patchAddress := bc.address()
            bc.appendInt(0)
         }

         if env[name] != () -> raise { kind: "CompilerError", reason: "compile: Function already defined", ast: stmt }

         env[stmt.name] := { kind: "Fn", params: params, offset: bc.address() }

         let fnEnv = { _localOffset: len(params) + 3, _numberOfArgs: len(params), ...env}

         params |> each(fn(param, offset) {
            fnEnv[param] := { kind: "LVar", offset: offset }
         })

         _compileBlock(bc, fnEnv, body)
      }
      | _ -> {
         if patchAddress != () -> {
            bc.patchInt(patchAddress, bc.address())
            patchAddress := ()
         }

         _compileStatement(bc, env, stmt)
      }
   )

   bc.code()
}

let _compileBlock(bc, env, stmts) {
   stmts |> each(fn(stmt) {
      _compileStatement(bc, env, stmt)
   })
}

let _compileStatement(bc, env, stmt) =
   match stmt
   | { kind: "Assign", name, value } -> {
      let var = env[name]

      if var == () -> raise { kind: "CompilerError", reason: "_compileStatement: Variable not found", ast: stmt }

      _compileIntExpr(bc, env, value)

      match var
      | { kind: "Var", offset } -> bc.appendInstruction(Bytecode.STORE)
      | { kind: "LVar", offset } -> bc.appendInstruction(Bytecode.STOREL)
      | _ -> raise { kind: "CompilerError", reason: "compile: Incompatible assignment", ast: stmt }
      bc.appendInt(var.offset)
   }
   | { kind: "Print", args } -> {
      args |> each(fn(arg) {
         let vtype = _compileExpr(bc, env, arg)

         match vtype
         | "Bool" -> bc.appendInstruction(Bytecode.PRINTB)
         | "Int" -> bc.appendInstruction(Bytecode.PRINTI)
         | "String" -> bc.appendInstruction(Bytecode.PRINTS)
         | _ -> raise { kind: "CompilerError", reason: "compile: Incompatible type", ast: stmt }
      })
   }
   | { kind: "Println", args } -> {
      args |> each(fn(arg) {
         let vtype = _compileExpr(bc, env, arg)

         match vtype
         | "Bool" -> bc.appendInstruction(Bytecode.PRINTB)
         | "Int" -> bc.appendInstruction(Bytecode.PRINTI)
         | "String" -> bc.appendInstruction(Bytecode.PRINTS)
         | _ -> raise { kind: "CompilerError", reason: "compile: Incompatible type", ast: stmt }
      })
      bc.appendInstruction(Bytecode.PRINTLN)
   }
   | { kind: "Return", expr } -> {
      _compileIntExpr(bc, env, expr)

      bc.appendInstruction(Bytecode.STOREL)
      bc.appendInt(env._numberOfArgs)
      bc.appendInstruction(Bytecode.RET)
      bc.appendInt(env._numberOfArgs)
   }
   | { kind: "Var", name, value } -> {
      if env[name] != () -> raise { kind: "CompilerError", reason: "compile: Variable already defined", ast: stmt }

      _compileIntExpr(bc, env, value)

      env[stmt.name] := { kind: "LVar", offset: env._localOffset }
      env._localOffset := env._localOffset + 1
   }
   | _ -> raise { kind: "CompilerError", reason: "_compileStatement: Not implemented", ast: stmt }

let _operatorMap = {
   Equals: [Bytecode.EQI, "Bool"],
   NotEquals: [Bytecode.NEQI, "Bool"],
   LessThan: [Bytecode.LTI, "Bool"],
   LessEquals: [Bytecode.LEI, "Bool"],
   GreaterThan: [Bytecode.GTI, "Bool"],
   GreaterEquals: [Bytecode.GEI, "Bool"],
   Add: [Bytecode.ADDI, "Int"],
   Subtract: [Bytecode.SUBTRACTI, "Int"],
   Multiply: [Bytecode.MULTIPLYI, "Int"],
   Divide: [Bytecode.DIVIDEI, "Int"],
   Modulus: [Bytecode.MODULUSI, "Int"]
}

let _compileExpr(bc, env, expr) =
   match expr
   | { kind: "Or", exprs } -> {
      if len(exprs) == 0 -> raise { kind: "CompilerError", reason: "_compileExpr: Or with no arguments", ast: expr }
       | len(exprs) == 1 -> _compileExpr(bc, env, exprs[0])
       | {
         let patches = []
         exprs |> each(fn(e) {
            _compileBoolExpr(bc, env, e)
            bc.appendInstruction(Bytecode.JMP_NEQ_ZERO)
            patches <! bc.address()
            bc.appendInt(0)
         })

         bc.appendInstruction(Bytecode.PUSHI)
         bc.appendInt(0)

         bc.appendInstruction(Bytecode.JMP)
         let patch = bc.address()
         bc.appendInt(0)

         patches |> each(fn(addr) {
            bc.patchInt(addr, bc.address())
         })

         bc.appendInstruction(Bytecode.PUSHI)
         bc.appendInt(1)

         bc.patchInt(patch, bc.address())

         "Bool"
      }
   }
   | { kind: "And", exprs } -> {
      if len(exprs) == 0 -> raise { kind: "CompilerError", reason: "_compileExpr: And with no arguments", ast: expr }
       | len(exprs) == 1 -> _compileExpr(bc, env, exprs[0])
       | {
         let patches = []
         exprs |> each(fn(e) {
            _compileBoolExpr(bc, env, e)
            bc.appendInstruction(Bytecode.JMP_EQ_ZERO)
            patches <! bc.address()
            bc.appendInt(0)
         })

         bc.appendInstruction(Bytecode.PUSHI)
         bc.appendInt(1)

         bc.appendInstruction(Bytecode.JMP)
         let patch = bc.address()
         bc.appendInt(0)

         patches |> each(fn(addr) {
            bc.patchInt(addr, bc.address())
         })

         bc.appendInstruction(Bytecode.PUSHI)
         bc.appendInt(0)

         bc.patchInt(patch, bc.address())

         "Bool"
      }
   }
   | { kind, lhs, rhs }  -> {
      let [op, vtype] = _operatorMap[kind]

      _compileIntExpr(bc, env, lhs)
      _compileIntExpr(bc, env, rhs)

      bc.appendInstruction(op)

      vtype
   }

   | { kind: "Call", name, args } -> {
      let f = env[name]

      if f == () -> raise { kind: "CompilerError", reason: "_compileExpr: Function not found", ast: expr }

      if f.kind != "Fn" -> raise { kind: "CompilerError", reason: "_compileExpr: Not a function", ast: expr }

      if len(args) != len(f.params) -> raise { kind: "CompilerError", reason: "_compileExpr: Wrong number of arguments", ast: expr }

      args |> each(fn(arg) {
         _compileIntExpr(bc, env, arg)
      })

      bc.appendInstruction(Bytecode.CALL)
      bc.appendInt(f.offset)

      "Int"
   }
   | { kind: "Identifier", value } -> {
      let var = env[value]

      match var
      | () -> raise { kind: "CompilerError", reason: "_compileExpr: Variable not found", ast: expr }
      | { kind: "Var", offset } -> {
         bc.appendInstruction(Bytecode.PUSH)
         bc.appendInt(var.offset)
      }
      | { kind: "LVar", offset } -> {
         bc.appendInstruction(Bytecode.PUSHL)
         bc.appendInt(var.offset)
      }
      | _ -> raise { kind: "CompilerError", reason: "_compileExpr: Not a variable", ast: expr }

      "Int"
   }
   | { kind: "LiteralBool", value } -> {
      bc.appendInstruction(Bytecode.PUSHI)
      bc.appendInt(if value -> 1 | 0)

      "Bool"
   }
   | { kind: "LiteralInt", value } -> {
      bc.appendInstruction(Bytecode.PUSHI)
      bc.appendInt(value)

      "Int"
   }
   | { kind: "LiteralString", value } -> {
      bc.appendInstruction(Bytecode.PUSHS)
      bc.appendInt(len(value))
      bc.appendString(value)

      "String"
   }
   | _ -> raise { kind: "CompilerError", reason: "_compileExpr: Not implemented", ast: expr }


let _compileBoolExpr(bc, env, expr) {
   let vtype = _compileExpr(bc, env, expr)

   if vtype != "Bool" -> raise { kind: "CompilerError", reason: "_compileIntExpr: not a Bool", ast: expr }
}

let _compileIntExpr(bc, env, expr) {
   let vtype = _compileExpr(bc, env, expr)

   if vtype != "Int" -> raise { kind: "CompilerError", reason: "_compileIntExpr: not an Int", ast: expr }
}

let compilerDis(input) =
   Parser.parse(input) |> compile() |> Bytecode.disassemble()

let _codegen() {
   let bc = ""

   {
      appendInstruction: fn(op) {
         bc := bc + str(op, false)
      },
      appendInt: fn(n) {
         bc := bc + str([char(n % 256), char((n / 256) % 256), char((n / 65536) % 256), char((n / 16777216) % 256)], false)
      },
      appendString: fn(s) {
         bc := bc + s
      },
      code: fn() bc,
      address: fn() len(bc),
      patchInt: fn(addr, n) {
         let patch = str([char(n % 256), char((n / 256) % 256), char((n / 65536) % 256), char((n / 16777216) % 256)], false)

         bc := bc[:addr] + patch + bc[(addr + 4):]
      }
   }
}
